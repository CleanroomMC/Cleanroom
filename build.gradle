buildscript {
    repositories {
        mavenLocal()
        maven {
            name = "Outlands"
            url = 'https://maven.outlands.top/releases/'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'top.outlands.gradle:ForgeGradle:6.0.+'
        classpath('top.outlands:artifactural:3.0.1') {
            transitive = false
        }
    }
}

plugins {
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.3"
    //id "com.palantir.git-version" version "4.2.0"
}
apply plugin: 'com.palantir.git-version' // Declared in buildSrc/build.gradle

import com.cleanroommc.gradle.helpers.ProjectConstants
import com.cleanroommc.gradle.helpers.tasks.Util


def props = project.properties
def sysProps = System.getProperties()
def details = versionDetails()

group = 'com.cleanroommc'

if (project.hasProperty("release")) {
    version = details.lastTag
} else if (!project.hasProperty("run_number")) {
    version = details.lastTag + "+build." + details.commitDistance
} else {
    version = details.lastTag + "+build." + details.commitDistance + ".run." + props.run_number
}

def version_txt = new File(project.projectDir, "version.txt")
version_txt.write(version.toString())

println "Setting up Cleanroom $version"
if (project.hasProperty("run_number")) println("On GitHub runs #" + props.run_number)
println "Java: ${ -> sysProps['java.version']} | JVM: ${ -> sysProps['java.vm.version']} | Vendor: ${ -> sysProps['java.vendor']} | Architecture: ${ -> sysProps['os.arch']}"

// Initialize buildSrc
Util.init()

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs = options.compilerArgs + ProjectConstants.COMPILER_JVM_ARGUMENTS
    options.encoding = 'UTF-8'
}

idea.project.settings.compiler.javac.javacAdditionalOptions = ProjectConstants.COMPILER_JVM_ARGUMENTS.join(' ')

tasks.register('setup') {
    group = 'setup'
    // These must be strings so that we can do lazy resolution. Else we need evaluationDependsOnChildren above
    dependsOn ':minecraft:extractMapped'
    dependsOn ':cleanroom:extractMapped'
}

subprojects {
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs += ProjectConstants.COMPILER_JVM_ARGUMENTS
            options.encoding = 'UTF-8'
        }
    }
}
